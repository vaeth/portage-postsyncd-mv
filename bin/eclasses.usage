#!/usr/bin/env perl
BEGIN { require 5.006_001 }
package Eclasses;
our $VERSION = 'v4.4.0';
eval { $VERSION = version->declare($VERSION) };
eval 'use version 0.77; $VERSION = version->declare($VERSION)' if($@);

use warnings;
use strict;
use integer;
use File::Find ();
use File::Spec ();
use Getopt::Long 2.24 ();
#use Pod::Usage ();  # needed for --man, --help, some errors
#use String::ShellQuote ();  # not mandatory but recommended: poor fallback
#use File::Which ();  # not mandatory but recommended; there are fallbacks

my $name = 'eclasses.usage';

=encoding UTF-8

=head1 NAME

eclasses.usage - Print how often eclasses are used in a certain repository

The output is in decreasing order of usage (alphabetically for same usage)

=head1 SYNOPSIS

B<eclasses.usage> [options] [man|help]

For an extended help type B<eclasses.usage man>

=head1 OPTIONS AND ARGUMENTS

=over 8

=item B<--help> or B<-h> or B<help>

Display brief help

=item B<--man> or B<-?> or B<man>

Display extended help as a manpage

=item B<--version> or B<-V>

Print version number

=item B<--minimal=>I<number> or B<-m> I<number>

Print only eclasses which are used at least I<number> times

=item B<--dir=>I<path> or B<-d> I<path>

Specify the root of the repository.
If not specified or empty, I<path> is guessed by heuristics, using B<eix>
(if available) B<PORTDIR> (if set), B<portageq> and
B<POSTSYNC_MAIN_REPOSITORY> (if set)

=item B<--print-dir> or B<-D>

Print the directory used for B<--dir> to stdout (without a newline) and exit

=item B<--only-names> or B<-o>

Output only the names of the used eclasses, not their usage number

=item B<--first> or B<-1>

Output only the first match

=item B<--time=>I<time> or B<-t> I<time>

Only count eclass if the corresponding eclass/*.eclass exists and has an
mtime not older than I<time>

=back

=head1 COPYRIGHT AND LICENSE

Copyright Martin VE<auml>th
This program is under the GPL v2 License

=head1 AUTHOR

Martin VE<auml>th E<lt>martin@mvath.deE<gt>

=cut

# Options

my $minimal = 0;
my $portdir = '';
my $only_names = '';
my $time = undef;
my $first = '';
my $printdir = '';

sub pod2usage {
	require Pod::Usage;
	&Pod::Usage::pod2usage
}

sub version {
	print($name, ' ', $VERSION, "\n");
	exit(0)
}

sub error {
	print(STDERR $name, ': ', @_, "\n")
}

sub fatal {
	&error;
	exit(1)
}

# use String::ShellQuote () and return whether successful

{ my $shellquote = undef;
sub use_shellquote {
	return $shellquote if(defined($shellquote));
	eval {
		require String::ShellQuote
	};
	$shellquote = !$@
}}

sub shell_quote_best_effort {
	(&use_shellquote() ?
		&String::ShellQuote::shell_quote_best_effort : &join_quoted)
}

# like join(' ', @_), but shell-quote arguments

sub join_quoted {
	my @r;
	for my $i (@_) {
		my $a = $i;
		$a =~ s{\'}{\'\\\'\'}g;
		$a = "'$a'";
		$a =~ s{(\A|[^\\])\'([\w\-\,\.\:\/]*)\'}{$1$2}gm;
		push(@r, ($a ne '') ? $a : "''")
	}
	join(' ', @r)
}

{
	my $devnull = undef;
sub devnull {
	return $devnull if(defined($devnull));
	$devnull = &shell_quote_best_effort(File::Spec->devnull())
}}

# use File::Which module

{
	my $which = undef;
sub which_which {
	return $which if(defined($which));
	eval {
		require File::Which
	};
	$which = ($@ ? '' : 1)
}}

sub have_prg {
	my ($a) = @_;
	if(&which_which()) {
		return defined(File::Which::which($a))
	}
	(system(&shell_quote_best_effort('command', '-v', $a) . ' >'
		. &devnull() . ' 2>&1') == 0)
}

# Check whether eix is available. If yes, also export PRINT_APPEND=
{
	my $have_eix = undef;
sub have_eix {
	return $have_eix if(defined($have_eix));
	$have_eix = &have_prg('eix');
	$ENV{'PRINT_APPEND'} = '' if($have_eix);
	$have_eix
}}

sub read_var {
	my ($varname) = @_;
	return $ENV{$varname} if(exists($ENV{$varname}));
	my $have_eix = &have_eix();
	my $cmd = &shell_quote_best_effort(($have_eix ?
		('eix', '--print') : ('portageq', 'envvar')), $varname)
		. ' 2>' . &devnull();
	my $result = `$cmd`;
	$result = '' unless(defined($result));
	chomp($result) unless($have_eix);
	$result
}

sub get_portdir {
	return $ENV{'PORTDIR'} if(exists($ENV{'PORTDIR'}));
	if(&have_eix()) {
		my $portdir = &read_var('PORTDIR');
		return $portdir if($portdir ne '')
	}
	my $eprefix = &read_var('EROOT');
	$eprefix = '' unless(defined($eprefix));
	my $rootdir = File::Spec->rootdir();
	my $erootdir = (($eprefix eq '') ? $rootdir :
		File::Spec->catdir($eprefix, $rootdir));
	my $main = &read_var('POSTSYNC_MAIN_REPOSITORY');
	$main = 'gentoo' unless(defined($main) && ($main ne ''));
	my $cmd = &shell_quote_best_effort('portageq',
			'get_repo_path', $erootdir, $main)
		. ' 2>' . &devnull();
	my $result = `$cmd`;
	$result = '' unless(defined($result));
	chomp($result);
	&fatal('failed to determine directory of main repository')
		unless($result ne '');
	$result
}

sub check_mtime {
	my ($eclass, $time) = @_;
	my $filename = File::Spec->catfile($portdir, 'eclass',
		$eclass . '.eclass');
	my $mtime = ((stat($filename))[9]);
	(defined($mtime) && ($mtime ne '') && ($mtime >= $time))
}

Getopt::Long::Configure(qw(gnu_getopt));
Getopt::Long::GetOptions(
	'help|h', sub { &pod2usage(0) },
	'man|?', sub { &pod2usage(-verbose => 2, -exit => 0) },
	'version|V', \&version,
	'minimal|m=i', \$minimal,
	'dir|d=s', \$portdir,
	'print-dir|D', \$printdir,
	'only-names|o', \$only_names,
	'first|1', \$first,
	'time|t=i', \$time
) || &pod2usage(2);
if(@ARGV) {
	&pod2usage(-verbose => 2, -exit => 0) if($ARGV[0] =~ m{^[mM?]});
	&pod2usage(0) if($ARGV[0] =~ m{^[hH]});
	&pod2usage(2)
}

my %eclasses = ();

$portdir = &get_portdir() unless(defined($portdir) && ($portdir ne ''));
if($printdir) {
	print($portdir);
	exit(0)
}
my $dir = File::Spec->catdir($portdir, 'metadata', 'md5-cache');
&fatal('not a directory: ' . $dir) unless(-d $dir);
File::Find::find({
	wanted => sub {
		return unless(-f $_);
		open(my $fh, '<', $_) || return;
		while(<$fh>) {
		next unless(s{^\_eclasses\_\=}{});
			my $eclass = '';
			for my $i (split) {
				$eclass = !$eclass;
				next unless($eclass);
				if(exists($eclasses{$i})) {
					++$eclasses{$i}
				} else {
					$eclasses{$i} = 1
				}
			}
		}
		close($fh)
	}
}, $dir);
my @eclasses = ();
for my $i (keys(%eclasses)) {
	my $count = $eclasses{$i};
	next unless($count >= $minimal);
	next if(defined($time) && !&check_mtime($i, $time));
	push(@eclasses, [ $i, $count ]);
	last if($first)
}
for my $i (sort {
		(-($a->[1] <=> $b->[1])) || ($a->[0] cmp $b->[0])
	} @eclasses) {
	if($only_names) {
		print($i->[0], "\n")
	} else {
		print(join(' ',@$i), "\n")
	}
}
